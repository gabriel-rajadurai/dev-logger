apply plugin: 'maven-publish'
apply from: 'creds.gradle'

def LIB_GROUP_ID = 'com.gabriel.devlogger'
def LIB_ARTIFACT_ID = 'dev-logger'
def LIB_VERSION = '1.0.1'

def githubProperties = new Properties()
githubProperties.load(new FileInputStream(rootProject.file("github.properties"))) //Set env variable GPR_USER & GPR_API_KEY if not adding a properties file

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier "sources"
}

afterEvaluate {
    publishing {
        publications {
            devLogger(MavenPublication) {
                groupId LIB_GROUP_ID
                artifactId LIB_ARTIFACT_ID
                version LIB_VERSION
                artifact("$buildDir/outputs/aar/devLogger-release.aar")
                artifact(sourceJar)

                pom.withXml {
                    def dependenciesNode = asNode().appendNode('dependencies')

                    //Iterate over the compile dependencies (we don't want the test ones), adding a <dependency> node for each
                    configurations.api.allDependencies.each {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
        repositories {
            maven {
                name = "DevLogger"
                url = uri("https://maven.pkg.github.com/gabriel-rajadurai/dev-logger")
                credentials {
                    username = USER_NAME
                    password = TOKEN
                }
            }
            maven {
                name = 'DevLoggerLocal'
                url = "file://${buildDir}/repo"
            }
        }
    }
}
